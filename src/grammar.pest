prog = { SOI ~ function_decl_list ~ stmt_list+ ~ EOI }

function_decl_list = { function_decl* }
function_decl      = { type ~ ident ~ "(" ~ argument_list? ~ ")" ~ braced_stmts }
argument           = { type ~ ident }
argument_list      = { argument ~ ("," ~ argument)* }

stmt         = _{ assignment | declaration | while | write | return | if }
stmt_list    =  { stmt+ }
braced_stmts = _{ "{" ~ stmt_list ~ "}" }

declaration = { type ~ ident ~ ("=" ~ expr)? ~ ";" }
assignment  = { ident ~ "=" ~ expr ~ ";" }
while       = { "tantque" ~ "(" ~ expr ~ ")" ~ braced_stmts }
write       = { "ecrire" ~ "(" ~ expr ~ ")" ~ ";" }
if          = { "si" ~ "(" ~ expr ~ ")" ~ braced_stmts }
return      = { "retourner" ~ expr ~ ";" }

type = { "entier" | "booleen" }

expr      = { prefix? ~ primary ~ (infix ~ prefix? ~ primary)* }
expr_list = { expr ~ ("," ~ expr)* }

infix = _{ add | sub | mul | div | pow | mod | eq | neq | lte | gte | lt | gt }
add   =  { "+" }
sub   =  { "-" }
mul   =  { "*" }
div   =  { "/" }
pow   =  { "^" }
mod   =  { "%" }
eq    =  { "==" }
neq   =  { "!=" }
lte   =  { "<=" }
gte   =  { ">=" }
lt    =  { "<" }
gt    =  { ">" }

prefix = _{ neg | not }
neg    =  { "-" }
not    =  { "!" }

primary       = _{ integer | boolean | read | random | function_call | ident | "(" ~ expr ~ ")" }
integer       = @{ ASCII_DIGIT+ }
boolean       =  { "Vrai" | "Faux" }
read          =  { "lire" ~ "(" ~ ")" }
random        =  { "aleatoire" ~ "(" ~ expr ~ ")" }
function_call =  { ident ~ "(" ~ expr_list? ~ ")" }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" | NEWLINE }

